{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LatPhysBase\n",
    "using LatticePhysics\n",
    "using HDF5"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Sites / Bonds"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function saveBonds(\n",
    "        bs :: Vector{B},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"bonds\"\n",
    "        ;\n",
    "        append :: Bool = false\n",
    "    ) where {LB,N,B<:Bond{LB,N}}\n",
    "    \n",
    "    # determine the mode based on if one wants to append stuff\n",
    "    if append\n",
    "        mode = \"r+\"\n",
    "    else\n",
    "        mode = \"w\"\n",
    "    end\n",
    "    \n",
    "    # open the file in mode\n",
    "    h5open(fn, mode) do ucfile\n",
    "        # create the group in which the bonds are saved\n",
    "        group_bonds = g_create(ucfile, group)\n",
    "        # save the parameters\n",
    "        attrs(group_bonds)[\"N\"] = Int64(N)\n",
    "        attrs(group_bonds)[\"L\"] = string(LB)\n",
    "        # save all from / to values (Int64 arrays)\n",
    "        group_bonds[\"from\"] = Int64[from(b) for b in bs]\n",
    "        group_bonds[\"to\"]   = Int64[to(b) for b in bs]\n",
    "        # save all wraps\n",
    "        if 0 < Int64(N)\n",
    "            for n in 1:Int64(N)\n",
    "                group_bonds[\"wrap_$(n)\"] = Int64[wrap(b)[n] for b in bs]\n",
    "            end\n",
    "        end\n",
    "        # save all labels\n",
    "        if LB <: Number\n",
    "            group_bonds[\"label\"] = [label(b) for b in bs]\n",
    "        else\n",
    "            group_bonds[\"label\"] = String[string(label(b)) for b in bs]\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # return nothing\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function loadBonds(\n",
    "        ::Type{BI},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"bonds\"\n",
    "    ) where {LBI,NI,BI<:Union{Bond{LBI,NI},Bond}}\n",
    "    \n",
    "    # read attribute data\n",
    "    attr_data = h5readattr(fn, group)\n",
    "    # determine N and L based on this\n",
    "    N  = attr_data[\"N\"]\n",
    "    LB = Meta.eval(Meta.parse(attr_data[\"L\"]))\n",
    "    \n",
    "    # load all remaining data\n",
    "    bd_from  = h5read(fn, group*\"/from\")\n",
    "    bd_to    = h5read(fn, group*\"/to\")\n",
    "    bd_label = h5read(fn, group*\"/label\")\n",
    "    \n",
    "    if N == 0\n",
    "        bd_wrap = [Tuple([]) for i in 1:length(bd_from)]\n",
    "    else\n",
    "        bd_wrap_parts = [\n",
    "            h5read(fn, group*\"/wrap_\"*string(j)) for j in 1:N\n",
    "        ]\n",
    "        bd_wrap = [Tuple([bd_wrap_parts[j][i] for j in 1:N]) for i in 1:length(bd_from)]\n",
    "    end\n",
    "    \n",
    "    # create list of bonds\n",
    "    bs = Bond{LB,N}[\n",
    "        newBond(Bond{LB,N}, bd_from[i], bd_to[i], LB(bd_label[i]), bd_wrap[i])\n",
    "        for i in 1:length(bd_from)\n",
    "    ]\n",
    "    \n",
    "    # return nothing\n",
    "    return bs\n",
    "end\n",
    "\n",
    "function loadBonds(\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"bonds\"\n",
    "    )\n",
    "    \n",
    "    return loadBonds(Bond, fn, group)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function saveSites(\n",
    "        ss :: Vector{S},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"sites\"\n",
    "        ;\n",
    "        append :: Bool = false\n",
    "    ) where {LS,D,S<:Site{LS,D}}\n",
    "    \n",
    "    # determine the mode based on if one wants to append stuff\n",
    "    if append\n",
    "        mode = \"r+\"\n",
    "    else\n",
    "        mode = \"w\"\n",
    "    end\n",
    "    \n",
    "    # open the file in mode\n",
    "    h5open(fn, mode) do ucfile\n",
    "        # create the group in which the bonds are saved\n",
    "        group_sites = g_create(ucfile, group)\n",
    "        # save the parameters\n",
    "        attrs(group_sites)[\"D\"] = Int64(D)\n",
    "        attrs(group_sites)[\"L\"] = string(LS)\n",
    "        # save all Positions (D dimensions)\n",
    "        if 0 < Int64(D)\n",
    "            for n in 1:Int64(D)\n",
    "                group_sites[\"point_$(n)\"] = Float64[point(s)[n] for s in ss]\n",
    "            end\n",
    "        end\n",
    "        # save all labels\n",
    "        if LS <: Number\n",
    "            group_sites[\"label\"] = [label(s) for s in ss]\n",
    "        else\n",
    "            group_sites[\"label\"] = String[string(label(s)) for s in ss]\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # return nothing\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function loadSites(\n",
    "        ::Type{SI},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"sites\"\n",
    "    ) where {LSI,DI,SI<:Union{Site{LSI,DI},Site}}\n",
    "    \n",
    "    # read attribute data\n",
    "    attr_data = h5readattr(fn, group)\n",
    "    # determine D and L based on this\n",
    "    D  = attr_data[\"D\"]\n",
    "    LS = Meta.eval(Meta.parse(attr_data[\"L\"]))\n",
    "    \n",
    "    # load all remaining data\n",
    "    st_label = h5read(fn, group*\"/label\")\n",
    "    \n",
    "    if D == 0\n",
    "        st_point = [Float64[] for i in 1:length(st_label)]\n",
    "    else\n",
    "        st_point_parts = [\n",
    "            h5read(fn, group*\"/point_\"*string(j)) for j in 1:D\n",
    "        ]\n",
    "        st_point = Vector{Float64}[Float64[st_point_parts[j][i] for j in 1:D] for i in 1:length(st_label)]\n",
    "    end\n",
    "    \n",
    "    # create list of sites\n",
    "    ss = Site{LS,D}[\n",
    "        newSite(Site{LS,D}, st_point[i], LS(st_label[i]))\n",
    "        for i in 1:length(st_label)\n",
    "    ]\n",
    "    \n",
    "    # return nothing\n",
    "    return ss\n",
    "end\n",
    "\n",
    "function loadSites(\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"sites\"\n",
    "    )\n",
    "    \n",
    "    return loadSites(Site, fn, group)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "lt = getLatticeOpen(getUnitcellSquare(Symbol), 2)\n",
    "saveBonds(bonds(lt), \"test.h5\")\n",
    "bonds(lt)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "loadBonds(\"test.h5\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "uc = getUnitcellKagome(Symbol)\n",
    "saveSites(sites(uc), \"test.h5\")\n",
    "sites(uc)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "loadSites(\"test.h5\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Unitcell"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function saveUnitcell(\n",
    "        uc :: U,\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"unitcell\"\n",
    "        ;\n",
    "        append :: Bool = false\n",
    "    ) where {LS,LB,D,N,S<:AbstractSite{LS,D},B<:AbstractBond{LB,N},U<:Unitcell{S,B}}\n",
    "    \n",
    "    # determine the mode based on if one wants to append stuff\n",
    "    if append\n",
    "        mode = \"r+\"\n",
    "    else\n",
    "        mode = \"w\"\n",
    "    end\n",
    "    \n",
    "    # determine the site and bond group names\n",
    "    group_sites = group*\"/sites\"\n",
    "    group_bonds = group*\"/bonds\"\n",
    "    \n",
    "    # open the file in mode\n",
    "    h5open(fn, mode) do ucfile\n",
    "        # create the top level group\n",
    "        group_uc = g_create(ucfile, group)\n",
    "        # save into the attributes in which groups the sites and bonds are stored\n",
    "        attrs(group_uc)[\"sites\"] = group_sites\n",
    "        attrs(group_uc)[\"bonds\"] = group_bonds\n",
    "        # save types into the attributes\n",
    "        attrs(group_uc)[\"site_type\"] = string(S)\n",
    "        attrs(group_uc)[\"bond_type\"] = string(B)\n",
    "        # number of bravais lattice dimensions\n",
    "        attrs(group_uc)[\"N\"] = Int64(N)\n",
    "        # save further attributes\n",
    "        \n",
    "        # write bravais lattice vectors\n",
    "        for i in 1:N\n",
    "            group_uc[\"a\"*string(i)] = latticeVectors(uc)[i]\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # save sites and bonds into the file\n",
    "    saveSites(sites(uc), fn, group_sites, append=true)\n",
    "    saveBonds(bonds(uc), fn, group_bonds, append=true)\n",
    "    \n",
    "    # return nothing\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function loadUnitcell(\n",
    "        ::Type{UC},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"unitcell\"\n",
    "    ) where {SUC<:AbstractSite,BUC<:AbstractBond,UC<:Union{Unitcell{SUC,BUC}, Unitcell}}\n",
    "        \n",
    "    # read the attribute data\n",
    "    attr_data = h5readattr(fn, group)\n",
    "    \n",
    "    # determine the bravais lattice dimension\n",
    "    N = attr_data[\"N\"]\n",
    "    \n",
    "    # determine the site and bond group names\n",
    "    group_sites = attr_data[\"sites\"]\n",
    "    group_bonds = attr_data[\"bonds\"]\n",
    "    \n",
    "    # load bravais lattice vectors\n",
    "    lattice_vectors = Vector{Float64}[]\n",
    "    for i in 1:N\n",
    "        push!(lattice_vectors, h5read(fn, group*\"/a\"*string(i)))\n",
    "    end\n",
    "    \n",
    "    # load the sites\n",
    "    site_list = loadSites(fn, group_sites)\n",
    "    # load the bonds\n",
    "    bond_list = loadBonds(fn, group_bonds)\n",
    "    \n",
    "    # determine site and bond type\n",
    "    S = typeof(site_list[1])\n",
    "    B = typeof(bond_list[1])\n",
    "    \n",
    "    # return new unitcell\n",
    "    return newUnitcell(\n",
    "        Unitcell{S,B},\n",
    "        lattice_vectors,\n",
    "        site_list,\n",
    "        bond_list\n",
    "    )\n",
    "end\n",
    "\n",
    "function loadUnitcell(\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"unitcell\"\n",
    "    )\n",
    "    \n",
    "    return loadUnitcell(Unitcell, fn, group)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "uc = getUnitcellHoneycomb(String, Int64)\n",
    "saveUnitcell(uc, \"test.h5\")\n",
    "uc"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "loadUnitcell(\"test.h5\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Lattice"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function saveLattice(\n",
    "        lt :: L,\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"lattice\"\n",
    "        ;\n",
    "        append :: Bool = false\n",
    "    ) where {LS,LB,D,N,S<:AbstractSite{LS,D},B<:AbstractBond{LB,N},SU,BU,U<:AbstractUnitcell{SU,BU},L<:Lattice{S,B,U}}\n",
    "    \n",
    "    # determine the mode based on if one wants to append stuff\n",
    "    if append\n",
    "        mode = \"r+\"\n",
    "    else\n",
    "        mode = \"w\"\n",
    "    end\n",
    "    \n",
    "    # determine the site and bond group names\n",
    "    group_sites    = group*\"/sites\"\n",
    "    group_bonds    = group*\"/bonds\"\n",
    "    group_unitcell = group*\"/unitcell\"\n",
    "    \n",
    "    # open the file in mode\n",
    "    h5open(fn, mode) do ltfile\n",
    "        # create the top level group\n",
    "        group_lt = g_create(ltfile, group)\n",
    "        # save into the attributes in which groups the sites and bonds are stored\n",
    "        attrs(group_lt)[\"sites\"]    = group_sites\n",
    "        attrs(group_lt)[\"bonds\"]    = group_bonds\n",
    "        attrs(group_lt)[\"unitcell\"] = group_unitcell\n",
    "        # save types into the attributes\n",
    "        attrs(group_lt)[\"site_type\"]     = string(S)\n",
    "        attrs(group_lt)[\"bond_type\"]     = string(B)\n",
    "        attrs(group_lt)[\"unitcell_type\"] = string(U)\n",
    "        # number of bravais lattice dimensions\n",
    "        attrs(group_lt)[\"N\"] = Int64(N)\n",
    "        # save further attributes\n",
    "        \n",
    "        # write bravais lattice vectors\n",
    "        for i in 1:N\n",
    "            group_lt[\"a\"*string(i)] = latticeVectors(lt)[i]\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # save sites, bonds and unitcell into the file\n",
    "    saveSites(sites(lt), fn, group_sites, append=true)\n",
    "    saveBonds(bonds(lt), fn, group_bonds, append=true)\n",
    "    saveUnitcell(unitcell(lt), fn, group_unitcell, append=true)\n",
    "    \n",
    "    # return nothing\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function loadLattice(\n",
    "        ::Type{LT},\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"lattice\"\n",
    "    ) where {SLT<:AbstractSite,BLT<:AbstractBond,ULT<:AbstractUnitcell,LT<:Union{Lattice{SLT,BLT,ULT},Lattice}}\n",
    "    \n",
    "    # read the attribute data\n",
    "    attr_data = h5readattr(fn, group)\n",
    "    \n",
    "    # determine the bravais lattice dimension\n",
    "    N = attr_data[\"N\"]\n",
    "    \n",
    "    # determine the site and bond group names\n",
    "    group_sites    = attr_data[\"sites\"]\n",
    "    group_bonds    = attr_data[\"bonds\"]\n",
    "    group_unitcell = attr_data[\"unitcell\"]\n",
    "    \n",
    "    # load bravais lattice vectors\n",
    "    lattice_vectors = Vector{Float64}[]\n",
    "    for i in 1:N\n",
    "        push!(lattice_vectors, h5read(fn, group*\"/a\"*string(i)))\n",
    "    end\n",
    "    \n",
    "    # load the sites\n",
    "    site_list = loadSites(fn, group_sites)\n",
    "    # load the bonds\n",
    "    bond_list = loadBonds(fn, group_bonds)\n",
    "    # load the unitcell\n",
    "    unitcell_loaded = loadUnitcell(fn, group_unitcell)\n",
    "    \n",
    "    # determine site and bond type\n",
    "    S = typeof(site_list[1])\n",
    "    B = typeof(bond_list[1])\n",
    "    U = typeof(unitcell_loaded)\n",
    "    \n",
    "    # return new unitcell\n",
    "    return newLattice(\n",
    "        Lattice{S,B,U},\n",
    "        lattice_vectors,\n",
    "        site_list,\n",
    "        bond_list,\n",
    "        unitcell_loaded\n",
    "    )\n",
    "end\n",
    "\n",
    "function loadLattice(\n",
    "        fn :: AbstractString,\n",
    "        group :: AbstractString = \"lattice\"\n",
    "    )\n",
    "    \n",
    "    return loadLattice(Lattice, fn, group)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "uc = getUnitcellHyperoctagon(Int64, Float64)\n",
    "lt = getLatticeOpen(uc, 10)\n",
    "saveLattice(lt, \"test.h5\")\n",
    "lt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "loadLattice(\"test.h5\")"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.0.0",
   "language": "julia",
   "name": "julia-1.0"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.0.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
